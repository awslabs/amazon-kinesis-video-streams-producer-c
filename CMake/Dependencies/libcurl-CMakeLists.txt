cmake_minimum_required(VERSION 3.6.3)

project(libcurl-download LANGUAGES C)

find_program(MAKE_EXE NAMES make)

if (DEFINED CMAKE_OSX_SYSROOT AND NOT CMAKE_OSX_SYSROOT STREQUAL "")
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -isysroot${CMAKE_OSX_SYSROOT}")
endif()

include(ExternalProject)

set(CMAKE_ARGS
        -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
        -DCMAKE_INSTALL_PREFIX=${OPEN_SRC_INSTALL_PREFIX}
        -DBUILD_CURL_EXE=OFF
        -DCMAKE_USE_LIBSSH2=OFF
        -DBUILD_TESTING=OFF
        -DHTTP_ONLY=ON)

# Note: Future Curl versions introduce BUILD_STATIC_LIBS flag
if (BUILD_STATIC)
    set(CMAKE_ARGS ${CMAKE_ARGS} -DBUILD_SHARED_LIBS=OFF)
else()
    set(CMAKE_ARGS ${CMAKE_ARGS} -DBUILD_SHARED_LIBS=ON)
endif()

# By default use openssl
if (USE_MBEDTLS)
    set(CMAKE_ARGS ${CMAKE_ARGS} -DCMAKE_USE_MBEDTLS=1)
endif()

# Tell Curl to find all its deps (OpenSSL/MbedTLS) in open-source directory
# (except when using pre-installed)
if (NOT OPENSSL_ROOT_DIR)
    set(CMAKE_ARGS
        ${CMAKE_ARGS}
        -DCMAKE_PREFIX_PATH=${OPEN_SRC_INSTALL_PREFIX}
        -DCMAKE_INSTALL_RPATH=$ORIGIN
    )
else()
    set(CMAKE_ARGS
        ${CMAKE_ARGS}
        -DCMAKE_INSTALL_RPATH=${OPENSSL_ROOT_DIR}/lib:$ORIGIN
    )
endif()

if (WIN32)
    set(CMAKE_ARGS ${CMAKE_ARGS} -DCMAKE_USE_SCHANNEL=1 -DCURL_STATIC_CRT=1)
endif()

ExternalProject_Add(project_libcurl
    GIT_REPOSITORY    https://github.com/curl/curl.git
    GIT_TAG           curl-7_74_0
    GIT_SHALLOW       TRUE
    GIT_PROGRESS      TRUE
    PREFIX            ${CMAKE_CURRENT_BINARY_DIR}/build
    CMAKE_ARGS        ${CMAKE_ARGS}
    BUILD_ALWAYS      TRUE
    TEST_COMMAND      "")
